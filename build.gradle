/*
 *
 *  * The MIT License
 *  *
 *  * Copyright {$YEAR} Apothesource, Inc.
 *  *
 *  * Permission is hereby granted, free of charge, to any person obtaining a copy
 *  * of this software and associated documentation files (the "Software"), to deal
 *  * in the Software without restriction, including without limitation the rights
 *  * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 *  * copies of the Software, and to permit persons to whom the Software is
 *  * furnished to do so, subject to the following conditions:
 *  *
 *  * The above copyright notice and this permission notice shall be included in
 *  * all copies or substantial portions of the Software.
 *  *
 *  * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 *  * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 *  * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 *  * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 *  * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 *  * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 *  * THE SOFTWARE.
 *
 */

plugins {
    id 'me.tatarka.retrolambda' version '3.1.0'
    id 'java'
    id 'maven'
    id 'signing'
}

sourceCompatibility = '1.8'

group = 'com.apothesource.pillfill'
archivesBaseName = 'pf-java-client'
version = '1.0.1-SNAPSHOT'


repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

retrolambda {
    //Pull the System environment settings if they exist- If not, use gradle.properties
    jdk System.getenv("JAVA8_HOME") ? System.getenv("JAVA8_HOME") : "${JAVA8_HOME}"
    oldJdk System.getenv("JAVA7_HOME") ? System.getenv("JAVA7_HOME") : "${JAVA7_HOME}"
    javaVersion JavaVersion.VERSION_1_7
}

configurations {
    provided
}
jar {
    manifest {
        attributes 'Main-Class': 'com.apothesource.pillfill.CommandlineClient'
    }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}
dependencies {
    compile 'com.google.code.gson:gson:2.3'
    compile 'org.cryptonode.jncryptor:jncryptor:1.2.0'
    compile 'io.reactivex:rxjava:1.0.11'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.google.dagger:dagger:2.0.1'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.google.guava:guava:18.0'

    provided 'com.google.dagger:dagger-compiler:2.0.1'

    testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'junit:junit:4.12'
}

test{
    logging.setLevel(LogLevel.INFO)
}
javadoc{
    failOnError false
    exclude "src/main/java/com/apothesource/pillfill/datamodel/**"
}

task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives javadocJar, sourcesJar
}
signing {
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'PillFill Java Client'
                packaging 'jar'
                description 'A mHealth library to simplify accessing patient prescriptions, drug information, and prescriber/pharmacy data.'
                url 'http://developer.pillfill.com/'

                scm {
                    connection 'scm:git:git://github.com/rammic/pf-java-client.git'
                    developerConnection 'scm:git:git://github.com/rammic/pf-java-client.git'
                    url 'https://github.com/rammic/pf-java-client'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://raw.githubusercontent.com/rammic/pf-java-client/master/license.txt'
                    }
                }

                developers {
                    developer {
                        id 'rammic'
                        name 'Michael Ramirez'
                        email 'michael@pillfill.com'
                    }
                }
            }
        }
    }
}